plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.0'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'checkstyle'
    id 'pmd'
}

group = 'com.musinsa'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

dependencies {

    // Spring Boot
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    // Embedded Tomcat
    runtimeOnly 'org.springframework.boot:spring-boot-starter-tomcat'

    // Database
    runtimeOnly 'com.h2database:h2'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    // ModelMapper
    implementation 'org.modelmapper:modelmapper:3.2.1'
}

// JUnit Platform 사용하도록 설정
test {
    useJUnitPlatform()
}

// 빌드 시 테스트가 실행되지 않도록 설정
tasks.withType(Test).configureEach {
    enabled = false
}

// Xlint:unchecked 설정
allprojects {
    tasks.withType(JavaCompile).tap {
        configureEach {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

// Checkstyle 설정
checkstyle {
    configFile = file("${rootDir}/config/checkstyle/google-checkstyle.xml")
    configProperties = ["suppressionFile": project(':').file('config/checkstyle/checkstyle-suppressions.xml')]
    ignoreFailures = false
    maxWarnings = 0
    reportsDir = file(project.layout.buildDirectory.dir("checkstyle-output").get().asFile.path)
    toolVersion = '10.20.0'
}

checkstyleMain {
    reports {
        xml.outputLocation = file("${checkstyle.reportsDir}/checkstyle-report.xml")
    }
}

// PMD 설정
pmd {
    ruleSetFiles = files("${rootDir}/config/pmd/custom-ruleset.xml")
    ruleSets = []
    consoleOutput = true
    ignoreFailures = false
    reportsDir = file(project.layout.buildDirectory.dir("pmd-output").get().asFile.path)
    toolVersion = '7.7.0'
}

pmdMain {
    reports {
        xml.outputLocation = file("${pmd.reportsDir}/pmd-report.xml")
        xml.required = true
    }
}